# VARIABLES
# =========
# sanity_points - Current SAN points for me
# san_lock - Set while waiting score update.
# adventure_points - Current adventure points for me

HANDSIZE=13;
DECKBOX_SIZE=(20,220,110);

# Servers
if(!port)
    port=29116;

product.image=(,);

# MENUS
# =========
GAME_MENU{"Main Menu - Play"}=(
    (1,"{hr}","",""),
    (1,"Select Investigator","","SelectInvestigator()"),
    (1,"{hr}","",""),
    (1,"Add 1 Adventure Point","/ap +1",""),
    (1,"Del 1 Adventure Point","/ap -1",""),
);

GAME_MENU{"A Card"}=(
    (1,"Play to Story Deck","Alt+Home",""),
    (1,"Bury in Story Deck","Alt+End",""),
);

GAME_MENU{"Story Deck"}=(
    ("{hr}","",""),
    ("Look","Ctrl+L",""),
    ("Shuffle","Ctrl+S",""),
);

#
# PlayStoryDeckMenu() - Generate menu for sites.
#
def PlayStoryDeckMenu
{
    push(m);
    m=(,);
    if(card.number!=NULL)
    {
        m=m+SingleCardMenu("discard");
    }

    m = m + GAME_MENU{"Story Deck"};

    Menu("Story Deck", m);
    m = pop();
}

# KEY BINDINGS
# =============
def MouseLeftClickStoryDeck
  PlayKeyControlKeyL();

def MouseLeftClickDiscardPile
  PlayKeyControlKeyL();

# Play to top of Story Deck
# e.g. Location or Travel Event
def Travel
{
    if(object.number)
    {
        if(card.number!=NULL)
        {
            TryTransferTo("deck.top",deck.objects[me]{"story deck"});
        }
    }
}

# Play to bottom of Story Deck
def Bury
{
    if(object.number)
    {
        if(card.number!=NULL)
        {
            TryTransferTo("deck.bottom",deck.objects[me]{"story deck"});
        }
    }
}

# Player announces a Pass.
# Same as Ctrl+E, just more specific
def Pass
{
    Say("{magenta}Pass");
    if(isfunction("EndOfTurnHook"))
        call("EndOfTurnHook",NULL);
    Send("PassTimer",NULL);
    Send("PlaySound","notify");
}

# Override Ctrl+S for cards
# Send to top of Story Deck
def PlayKeyAltKeyHome
    Travel();

def PlayKeyAltKeyEnd
    Bury();

# FUNCTIONS
# =========

def NewDeck
{
    return((("deck",(,)),("investigators",(,)),));
}

def InitializeLocationTables
{
    rules.category=(,);

    rules.coordinate=(,);
}

#   default, TODO modify by investigators
def InitializeGame
{
    adventure_points = 0;
    sanity_points = 13;
    sanity_lock = 0;
}

#
# StartGame() - What to do when game begins.
#
def StartGame 
{
    Send("UploadDeck",decks{deck.name});
    Send("ShuffleDeck",deck.objects[me]{"deck"});
    Send("ShuffleDeck",deck.objects[me]{"investigators"});

    SelectInvestigator();
}

#
# EndGame() - Game over.
#
def EndGame
{
}

#
# RefreshScore() - Send a score update.
#
def RefreshScore
{
    Send("Score",(me,(("SP",sanity_points),("AP",adventure_points))));
}

# Search the deck for an Investigator, select, put it into play
def SelectInvestigator
{
    push(s);

    s=unique(select('Attr("type",#)=="Investigator"',decks{deck.name}{"investigators"}));
    SelectCard(s,'Send("Transfer",(1,("deck.search",deck.objects[me]{"investigators"},#),("play",0)))','("deck.search",deck.objects[me]{"investigators"},#)');
    s = pop();
}

#
# HOOKS
# =====

#
# ScoreHook(player,scorestruct)
#
def ScoreHook
{
    if(ARG[0]==me)
    {
        push(s);
        s=ARG[1];
        if(s{"AP"}!=NULL)
            adventure_points=toint(s{"AP"});

        if(san_lock)
            san_lock=san_lock-1;

        if(san_lock==0)
        {
            if(s{"SP"}!=NULL)
                sanity_points=s{"SP"};
        }

        s=pop();
    }
}


#
# ScoreToText(score structure) - Convert a score structure to string.
# 
def ScoreToText_disable
{
    push(s);
    s="";

    s=s+"San "+toint(ARG{"SP"});
    if(ARG{"AP"})
    {
        s="AP. "+toint(ARG{"AP"})+" "+s;
    }

    return(s);
    s=pop();
}

# InputHook(input)
#   Set San Points
#
def InputHook
{
    return(0);
    if(flag.playing)
    {
        push(s);
        push(d);

        d=NULL;
        s=toint(sanity_points);

        if(left(ARG,1)=="-" && tostr(toint(ARG))==ARG)
            d=toint(ARG);
        else if(left(ARG,1)=="+" && tostr(toint(substr(ARG,1)))==substr(ARG,1))
            d=toint(substr(ARG,1));
        else if(tostr(toint(ARG))==ARG)
            d=toint(ARG)-s;

        if(d != NULL)
        {
            s=s+d;
            sanity_points=s;
            RefreshScore();

            if(d >= 0)
                d="+"+d;
            Say("{orange}Sanity ("+d+") : "+s);

            return(1);
        }

        d=pop();
        s=pop();
    }
}


#
# COMMANDS
# ==========

def PlayCommandAp
{
    push(d);

    if(length(ARG))
    {
        d = toint(ARG[0]);

        adventure_points = adventure_points + d;
        Say("{blue}Adventure Points ("+d+") : "+ adventure_points);
        RefreshScore();
    }
    else
        Msg("{red}Missing arguments.");

    d = pop();
}

def PlayCommandPass
    Pass();


#
# LAYOUT
# ==========
# StacksByGroup(group index,card index,x,y,xoffset,yoffset,stack x-step,stack y-step):
#  (x,y) = (  x + (group_index * stack_xstep) + (card_index * xoffset),
#             y + (group_index * stack_ystep) + (card_index * yoffset))
#
# Investigator in the middle of your play area.
# Adventures stacked underneath the investigator
# Threat above the investigator (don't group by name, tells opponent too much)
# Allies horizontally to the right (no point in grouping by name)
# Tomes & Artifacts horizontally to the left
# All the rest stacked next to the story deck
#
def InitializeLocationTables
{
    rules.category=(
        ( 'type=="Investigator" || type=="Adventure"','("adventure_stack","")'),
        ( 'type=="Monster"','("threat","")'),
        ( 'type=="Ally"','("ally","")'),
        ( 'type=="Artifact" || type=="Tome" || type=="Spell"','("permanent",name(card))'),
        ( 'findspace.facedown','("facedown","")'),
        ( '1','("other","")')
    );

    rules.coordinate=(
        (
            ("adventure_stack",),
            (
                (("StacksByGroup",table.width/3, table.height/4, 6, 16, 0, 0),),
                (("OffscreenEnabled","yes"),),
            )
        ),
        (
            ("threat",),
            (
                (("StacksByGroup", table.width/3 + card.xstep + 6, 16, card.xstep/2, 0, 0, 0),),
                (("OffscreenEnabled","yes"),),
            )
        ),
        (
            ("ally",),
            (
                (("StacksByGroup", table.width/3 + card.xstep + 6, table.height/4, card.xstep/2, 0, 0, 0),),
                (("OffscreenEnabled","yes"),),
            )
        ),
        (
            ("permanent",),
            (
                (("StacksByGroup", 6, table.height/4, 6, 16, card.xstep/2, 0),),
                (("OffscreenEnabled","yes"),),
                (("AutoRaiseGroupTail","yes"),),
            )
        ),
        (
            ("other",),
            (
                (("StacksByGroup", 160, 16, 6, 16, 0, 0),),
                (("AutoRaiseGroupTail","yes"),),
            )
        ),
    );
}
